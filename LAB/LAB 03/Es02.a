#Esercizio 2
#inserire una sequenza di numeri (letti come caratteri) fintanto
#che non si inserisce un carattere non numerico o un invio.
#Alla fine stampare il numero inserito

.data
messageInput:	.asciiz "Inserisci un numero: "
messageError:	.asciiz "\nL'input non è un numero valido."
messageOk:		.asciiz "L'input è corretto."
messageEmpty:	.asciiz "Non è stato inserito nessun numero!"
messageErr2:	.asciiz "\nIl numero inserito è troppo grande"
	
	.text
	.globl main
	.ent main

main:
	la $a0, messageInput
	li $v0, 4
	syscall
	li $t0, 0		#contatore numeri di caratteri interi
	li $s0, 0		#accumulatore
	
loop:	
	li $v0, 12		#lettura di un carattere
	syscall
	beq $v0, '\n', exitLoop
	blt $v0, '0', notANumber
	bgt $v0, '9', notANumber
	addi $t0, $t0, 1
	sub $t1, $v0, '0'
	mul $s0, $s0, 10
	add $s0, $s0, $t1
	bgt $s0, 0x0FFFFFFF, overflow
	j loop

exitLoop:	
	beq $t0, 0, noInput
	move $a0, $s0
	li $v0, 1
	syscall 
	la $a0, messageOk
	j printMessage

noInput:	
	la $a0, messageEmpty
	j printMessage
	
notANumber:	
	la $a0, messageError
	j printMessage

overflow:
	la $a0, messageErr2

printMessage:	
	li $v0, 4
	syscall
	
	li $v0, 10
	syscall
	.end main





















